
<!DOCTYPE html>
<html>
<head>
    <meta charset='utf-8'/>
    <script type="text/javascript" src="../libs/libs.js"></script>
    <script type="text/javascript" src="../libs/primitives.js"></script>
    <script type="text/javascript" src="icosahedra.js"></script>

    <script id="light-shader-vs" type="x-shader/x-vertex">
        attribute vec3 position;
        attribute vec3 color;
        attribute vec3 normal;

        uniform mat4 Pmatrix;
        uniform mat4 Vmatrix;
        uniform mat4 Mmatrix;
        uniform vec3 wsLightPosition;

        varying vec3 vColor;
        varying vec3 wsPosition;
        varying vec3 csNormal;
        varying vec3 csEyeDirection;
        varying vec3 csLightDirection;

        void main(void) {
        // Output position of the vertex, in clip space : MVP * position
        gl_Position = Pmatrix*Vmatrix*Mmatrix*vec4(position, 1.);

        // Position of the vertex, in worldspace : M * position
        wsPosition = (Mmatrix * vec4(position,1)).xyz;

        // Vector that goes from the vertex to the camera, in camera space.
        // In camera space, the camera is at the origin (0,0,0).
        vec3 csPosititon = ( Vmatrix * Mmatrix * vec4(position,1)).xyz;
        csEyeDirection = vec3(0,0,0) - csPosititon;

        // Vector that goes from the vertex to the light, in camera space. M is ommited because it's identity.
        vec3 csLightPosition = ( Vmatrix * vec4(wsLightPosition,1)).xyz;
        csLightDirection = csLightPosition + csEyeDirection;

        // Normal of the the vertex, in camera space
        csNormal = ( Vmatrix * Mmatrix * vec4(normal,0)).xyz; // Only correct if ModelMatrix does not scale the model ! Use its inverse transpose if not.

        vColor=color;

        }
    </script>

    <script id="light-shader-fs" type="x-shader/x-fragment">
        precision mediump float;

        uniform mat4 Vmatrix;
        uniform mat4 Mmatrix;
        uniform vec3 wsLightPosition;

        varying vec3 vColor;
        varying vec3 wsPosition;
        varying vec3 csNormal;
        varying vec3 csEyeDirection;
        varying vec3 csLightDirection;

        void main(void) {
        // Light emission properties
        // You probably want to put them as uniforms
        vec3 lColor = vec3(1,1,1);
        float lPower = 20.0;

        // Material properties
        vec3 MaterialDiffuseColor = vColor;
        vec3 MaterialAmbientColor = vec3(0.3,0.3,0.3) * MaterialDiffuseColor;
        vec3 MaterialSpecularColor = vec3(0.3,0.3,0.3);

        // Distance to the light
        float distance = length( wsLightPosition - wsPosition );

        // Normal of the computed fragment, in camera space
        vec3 n = normalize( csNormal );
        // Direction of the light (from the fragment to the light)
        vec3 l = normalize( csLightDirection );
        // Cosine of the angle between the normal and the light direction,
        // clamped above 0
        //  - light is at the vertical of the triangle -> 1
        //  - light is perpendicular to the triangle -> 0
        //  - light is behind the triangle -> 0
        float cosTheta = min(max(dot( n,l ),0.), 1.);
        // Eye vector (towards the camera)
        vec3 E = normalize(csEyeDirection);
        // Direction in which the triangle reflects the light
        vec3 R = reflect(-l,n);
        // Cosine of the angle between the Eye vector and the Reflect vector,
        // clamped to 0
        //  - Looking into the reflection -> 1
        //  - Looking elsewhere -> < 1
        float cosAlpha = min(max(dot( E,R ),0.), 1.);


        gl_FragColor = vec4(
        // Ambient : simulates indirect lighting
        MaterialAmbientColor +
        // Diffuse : "color" of the object
        MaterialDiffuseColor * lColor * lPower * cosTheta / (distance*distance) +
        // Specular : reflective highlight, like a mirror
        MaterialSpecularColor * lColor * lPower * pow(cosAlpha,5.) / (distance*distance),1.);
        }
    </script>
</head>
<body style='margin:0px' onload='main()'>
<canvas id='canvas' style='position: absolute; background-color: black;'></canvas>
</body>
</html>